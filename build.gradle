import org.apache.tools.ant.filters.ReplaceTokens

group 'com.lousylynx'
version '1.0'

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    fatjar
}

dependencies {
//    testCompile group: 'junit', name: 'junit', version: '4.12'
    fatjar 'commons-cli:commons-cli:1.4'
    compileOnly 'org.projectlombok:lombok:1.16.18'

    configurations.compile.extendsFrom(configurations.fatjar)
}

jar {
    include '**'
    exclude 'com/lousylynx/asciidots/cli/**'
}

task sourcesForRelease(type: Copy) {
    from 'src/main/java'
    into 'build/adjustedSrc'
    filter(ReplaceTokens, tokens: [VERSION: project.version])
}

task compileForRelease(type: JavaCompile, dependsOn: sourcesForRelease) {
    source = sourcesForRelease.destinationDir
    classpath = sourceSets.main.compileClasspath
    destinationDir = file('build/adjustedClasses')
}

task cliJar(type: Jar, dependsOn: [':compileForRelease', ':sourcesForRelease']) {
    manifest {
        attributes 'Main-Class': 'com.lousylynx.asciidots.cli.ADCLI'
    }

    from configurations.fatjar.collect { it.isDirectory() ? it : zipTree(it) }
    //from sourceSets.main.output
    from compileForRelease.destinationDir

    //print(compileForRelease.main.output)

    include '**'
    exclude 'META-INF/**'
    include 'META-INF/MANIFEST.MF'

    classifier 'cli'
}

artifacts {
    archives cliJar
    archives jar
}

idea {
    module {
        // if you hate browsing Javadoc
        downloadJavadoc = true

        // and love reading sources :)
        downloadSources = true
    }
}